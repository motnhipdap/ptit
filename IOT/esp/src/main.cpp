// ===== B·∫¨T C√ÅC T√çNH NƒÇNG =====
#define ENABLE_USER_AUTH // B·∫≠t x√°c th·ª±c ng∆∞·ªùi d√πng v·ªõi Firebase
#define ENABLE_DATABASE  // B·∫≠t k·∫øt n·ªëi v·ªõi Firebase Database

// ===== TH√äM C√ÅC TH∆Ø VI·ªÜN C·∫¶N THI·∫æT =====
#include <Arduino.h>          // Th∆∞ vi·ªán c∆° b·∫£n c·ªßa Arduino
#include <WiFi.h>             // Th∆∞ vi·ªán WiFi ƒë·ªÉ k·∫øt n·ªëi m·∫°ng
#include <WiFiClientSecure.h> // Th∆∞ vi·ªán k·∫øt n·ªëi an to√†n (SSL/TLS)
#include <FirebaseClient.h>   // Th∆∞ vi·ªán Firebase ch√≠nh
#include <FirebaseJSON.h>     // Th∆∞ vi·ªán x·ª≠ l√Ω d·ªØ li·ªáu JSON
#include <Adafruit_Sensor.h>  // Th∆∞ vi·ªán c·∫£m bi·∫øn Adafruit
#include <Adafruit_BMP085.h>  // Th∆∞ vi·ªán c·∫£m bi·∫øn nhi·ªát ƒë·ªô & √°p su·∫•t BMP180
#include <HX711.h>            // Th∆∞ vi·ªán c·∫£m bi·∫øn c√¢n (Load Cell)
#include <time.h>             // Th∆∞ vi·ªán x·ª≠ l√Ω th·ªùi gian
#include <EEPROM.h>           // Th∆∞ vi·ªán l∆∞u tr·ªØ d·ªØ li·ªáu vƒ©nh vi·ªÖn
#include "esp_system.h"       // Th∆∞ vi·ªán h·ªá th·ªëng ESP32
#include "esp_task_wdt.h"     // Th∆∞ vi·ªán Watchdog Timer (gi√°m s√°t h·ªá th·ªëng)

// ===== ƒê·ªäNH NGHƒ®A C√ÅC CH√ÇN K·∫æT N·ªêI =====
#define BMP_I2C_ADDR 0x76    // ƒê·ªãa ch·ªâ I2C c·ªßa c·∫£m bi·∫øn BMP180
#define PIR_PIN 25           // Ch√¢n k·∫øt n·ªëi c·∫£m bi·∫øn chuy·ªÉn ƒë·ªông PIR
#define LED_PIN 2            // Ch√¢n LED b√°o hi·ªáu (LED built-in)
#define LOADCELL_DOUT_PIN 32 // Ch√¢n d·ªØ li·ªáu c·ªßa c·∫£m bi·∫øn c√¢n
#define LOADCELL_SCK_PIN 33  // Ch√¢n xung clock c·ªßa c·∫£m bi·∫øn c√¢n
#define IR_PIN 35            // Ch√¢n analog ƒë·ªçc c·∫£m bi·∫øn h·ªìng ngo·∫°i

// ===== C·∫§U H√åNH B·ªò NH·ªö EEPROM =====
// EEPROM d√πng ƒë·ªÉ l∆∞u d·ªØ li·ªáu kh√¥ng m·∫•t khi t·∫Øt ngu·ªìn
#define EEPROM_SIZE 512             // K√≠ch th∆∞·ªõc EEPROM: 512 bytes
#define LOADCELL_CAL_ADDR 0         // ƒê·ªãa ch·ªâ l∆∞u hi·ªáu chu·∫©n c√¢n (4 bytes)
#define IR_THRESHOLDS_ADDR 10       // ƒê·ªãa ch·ªâ l∆∞u ng∆∞·ª°ng IR (4 bytes)
#define EEPROM_INITIALIZED_ADDR 500 // ƒê·ªãa ch·ªâ l∆∞u c·ªù ƒë√£ kh·ªüi t·∫°o
#define EEPROM_MAGIC_NUMBER 0xBEE5  // S·ªë ƒë·∫∑c bi·ªát ƒë·ªÉ ki·ªÉm tra EEPROM h·ª£p l·ªá

// ===== C·∫§U H√åNH WATCHDOG TIMER =====
// Watchdog s·∫Ω reset h·ªá th·ªëng n·∫øu ch∆∞∆°ng tr√¨nh b·ªã treo
#define WDT_TIMEOUT 30 // Th·ªùi gian timeout: 30 gi√¢y

// ===== TH√îNG TIN WIFI =====
#define WIFI_SSID "Sc"           // T√™n m·∫°ng WiFi
#define WIFI_PASSWORD "12345678" // M·∫≠t kh·∫©u WiFi

// ===== TH√îNG TIN FIREBASE =====
#define Web_API_KEY "AIzaSyACbzuHAKUuOTfa9kIZ61c_g1lXq_C531s"                                          // API Key c·ªßa Firebase
#define DATABASE_URL "https://beehive-monitor-377f2-default-rtdb.asia-southeast1.firebasedatabase.app" // URL Database
#define USER_EMAIL "thanhcong19092004@gmail.com"                                                       // Email ƒëƒÉng nh·∫≠p Firebase
#define USER_PASSWORD "12345678"                                                                       // M·∫≠t kh·∫©u ƒëƒÉng nh·∫≠p Firebase

// ===== C√ÄI ƒê·∫∂T ƒê·ªíNG H·ªí TH·ªúI GIAN =====
// NTP = Network Time Protocol (l·∫•y gi·ªù t·ª´ internet)
#define NTP_SERVER "pool.ntp.org" // M√°y ch·ªß NTP
#define GMT_OFFSET_SEC 25200      // M√∫i gi·ªù Vi·ªát Nam (GMT+7) = 7*3600 gi√¢y
#define DAYLIGHT_OFFSET_SEC 0     // Kh√¥ng s·ª≠ d·ª•ng gi·ªù m√πa h√®

// ===== C·∫§U H√åNH C·∫¢M BI·∫æN BMP180 (Nhi·ªát ƒë·ªô & √Åp su·∫•t) =====
#define SEALEVELPRESSURE_HPA (1013.25) // √Åp su·∫•t m·ª±c n∆∞·ªõc bi·ªÉn chu·∫©n
#define TEMP_THRESHOLD_HIGH 37.78      // Ng∆∞·ª°ng nhi·ªát ƒë·ªô cao (100¬∞F)
#define TEMP_THRESHOLD_LOW 10          // Ng∆∞·ª°ng nhi·ªát ƒë·ªô th·∫•p (50¬∞F)

// ===== C·∫§U H√åNH C·∫¢M BI·∫æN H·ªíNG NGO·∫†I IR (ƒê·∫øm ong ra v√†o) =====
#define DEFAULT_IR_THRESHOLDS 500     // Ng∆∞·ª°ng m·∫∑c ƒë·ªãnh ƒë·ªÉ ph√°t hi·ªán ong
#define DETECTION_DELAY 50            // Th·ªùi gian ch·ªù gi·ªØa 2 l·∫ßn ph√°t hi·ªán (ms)
#define CURRENT_UPDATE_INTERVAL 60000 // C·∫≠p nh·∫≠t l√™n Firebase m·ªói 60 gi√¢y
#define DAILY_RESET_HOUR 0            // Gi·ªù reset b·ªô ƒë·∫øm h√†ng ng√†y (0h)

// ===== C·∫§U H√åNH C√ÇN (Load Cell) =====
#define MEASURE_INTERVAL 1000      // ƒê·ªçc c√¢n m·ªói 1 gi√¢y
#define UPLOAD_INTERVAL 3600000    // T·∫£i l√™n Firebase m·ªói gi·ªù (3600000ms)
#define SAMPLE_SIZE 10             // ƒê·ªçc 10 l·∫ßn r·ªìi t√≠nh trung b√¨nh (ch√≠nh x√°c h∆°n)
#define EMPTY_HIVE_WEIGHT 5000     // Tr·ªçng l∆∞·ª£ng t·ªï ong r·ªóng: 5kg
#define WEIGHT_CHANGE_ALERT 2000   // C·∫£nh b√°o n·∫øu thay ƒë·ªïi > 2kg
#define DEFAULT_CALIBRATION -100.8 // Gi√° tr·ªã hi·ªáu chu·∫©n m·∫∑c ƒë·ªãnh

// ===== C·∫§U H√åNH C·∫¢M BI·∫æN CHUY·ªÇN ƒê·ªòNG PIR (Ph√°t hi·ªán x√¢m nh·∫≠p) =====
#define MOTION_COOLDOWN 5000     // Ch·ªù 5 gi√¢y gi·ªØa 2 l·∫ßn ph√°t hi·ªán
#define MOTION_DURATION_MIN 2000 // Chuy·ªÉn ƒë·ªông > 2 gi√¢y m·ªõi ghi nh·∫≠n
#define DETECTION_WINDOW 1000    // C·ª≠a s·ªï th·ªùi gian 1 gi√¢y
#define SAMPLES_REQUIRED 3       // C·∫ßn 3 l·∫ßn ƒë·ªçc HIGH trong c·ª≠a s·ªï

// ===== C·∫§U H√åNH K·∫æT N·ªêI FIREBASE =====
#define FIREBASE_MAX_RETRIES 3       // Th·ª≠ l·∫°i t·ªëi ƒëa 3 l·∫ßn n·∫øu l·ªói
#define FIREBASE_RETRY_INTERVAL 1000 // Ch·ªù 1 gi√¢y gi·ªØa m·ªói l·∫ßn th·ª≠

// ===== C√ÅC ƒê·ªêI T∆Ø·ª¢NG FIREBASE =====
FirebaseApp app;                      // ·ª®ng d·ª•ng Firebase ch√≠nh
WiFiClientSecure ssl_client;          // Client k·∫øt n·ªëi an to√†n (HTTPS)
using AsyncClient = AsyncClientClass; // ƒê·ªãnh nghƒ©a ki·ªÉu AsyncClient
AsyncClient aClient(ssl_client);      // Client b·∫•t ƒë·ªìng b·ªô
RealtimeDatabase Database;            // Database realtime c·ªßa Firebase
AsyncResult dbResult;                 // K·∫øt qu·∫£ tr·∫£ v·ªÅ t·ª´ Firebase
bool firebaseConnected = false;       // C·ªù ki·ªÉm tra ƒë√£ k·∫øt n·ªëi Firebase ch∆∞a

// ===== X√ÅC TH·ª∞C NG∆Ø·ªúI D√ôNG =====
UserAuth user_auth(Web_API_KEY, USER_EMAIL, USER_PASSWORD); // Th√¥ng tin ƒëƒÉng nh·∫≠p

// ===== BI·∫æN L∆ØU TH√îNG TIN NG∆Ø·ªúI D√ôNG =====
String uid; // UID (User ID) c·ªßa ng∆∞·ªùi d√πng Firebase

// ===== C√ÅC ƒê·ªêI T∆Ø·ª¢NG C·∫¢M BI·∫æN =====
Adafruit_BMP085 bmp; // ƒê·ªëi t∆∞·ª£ng c·∫£m bi·∫øn BMP180 (nhi·ªát ƒë·ªô & √°p su·∫•t)
HX711 loadcell;      // ƒê·ªëi t∆∞·ª£ng c·∫£m bi·∫øn c√¢n (Load Cell)

// ===== D·ªÆ LI·ªÜU C·∫¢M BI·∫æN BMP180 =====
// Struct (c·∫•u tr√∫c) l∆∞u tr·ªØ t·∫•t c·∫£ th√¥ng tin li√™n quan ƒë·∫øn BMP180
struct
{
    float temperature = 0;        // Nhi·ªát ƒë·ªô hi·ªán t·∫°i (¬∞C)
    float pressure = 0;           // √Åp su·∫•t hi·ªán t·∫°i (hPa)
    unsigned long lastRead = 0;   // Th·ªùi ƒëi·ªÉm ƒë·ªçc l·∫ßn cu·ªëi (ms)
    unsigned long lastUpload = 0; // Th·ªùi ƒëi·ªÉm upload l·∫ßn cu·ªëi (ms)
    bool initialized = false;     // ƒê√£ kh·ªüi t·∫°o c·∫£m bi·∫øn ch∆∞a?
    bool error = false;           // C√≥ l·ªói kh√¥ng?
} bmpData;                        // T√™n bi·∫øn: bmpData

// ===== D·ªÆ LI·ªÜU C·∫¢M BI·∫æN H·ªíNG NGO·∫†I IR =====
// D√πng ƒë·ªÉ ƒë·∫øm s·ªë l·∫ßn ong ra v√†o t·ªï
struct
{
    unsigned long lastDetectionTime = 0; // Th·ªùi ƒëi·ªÉm ph√°t hi·ªán l·∫ßn cu·ªëi
    unsigned long lastUploadTime = 0;    // Th·ªùi ƒëi·ªÉm upload l·∫ßn cu·ªëi
    uint32_t activityCount = 0;          // T·ªïng s·ªë l·∫ßn ong ra v√†o
    uint32_t lastUploadedCount = 0;      // S·ªë ƒë√£ upload l·∫ßn tr∆∞·ªõc
    int thresholds = 500;                // Ng∆∞·ª°ng ƒë·ªÉ ph√°t hi·ªán (m·∫∑c ƒë·ªãnh 500)
    bool initialized = false;            // ƒê√£ kh·ªüi t·∫°o ch∆∞a?
    bool error = false;                  // C√≥ l·ªói kh√¥ng?
    unsigned long lastRead = 0;          // Th·ªùi ƒëi·ªÉm ƒë·ªçc l·∫ßn cu·ªëi
} irData;                                // T√™n bi·∫øn: irData

// ===== D·ªÆ LI·ªÜU C·∫¢M BI·∫æN PIR =====
// D√πng ƒë·ªÉ ph√°t hi·ªán chuy·ªÉn ƒë·ªông (k·∫ª x√¢m nh·∫≠p)
struct
{
    const int READING_HISTORY_SIZE = 10; // L∆∞u 10 l·∫ßn ƒë·ªçc g·∫ßn nh·∫•t
    bool readingHistory[10];             // M·∫£ng l∆∞u l·ªãch s·ª≠ ƒë·ªçc (HIGH/LOW)
    int readingIndex = 0;                // V·ªã tr√≠ hi·ªán t·∫°i trong m·∫£ng
    unsigned long lastReadingTime = 0;   // Th·ªùi ƒëi·ªÉm ƒë·ªçc l·∫ßn cu·ªëi
    unsigned long lastValidMotion = 0;   // Th·ªùi ƒëi·ªÉm ph√°t hi·ªán chuy·ªÉn ƒë·ªông h·ª£p l·ªá
    unsigned long lastMotionTime = 0;    // Th·ªùi ƒëi·ªÉm c√≥ chuy·ªÉn ƒë·ªông
    unsigned long motionStartTime = 0;   // Th·ªùi ƒëi·ªÉm b·∫Øt ƒë·∫ßu chuy·ªÉn ƒë·ªông
    unsigned long lastStatusUpdate = 0;  // Th·ªùi ƒëi·ªÉm c·∫≠p nh·∫≠t tr·∫°ng th√°i
    bool motionActive = false;           // ƒêang c√≥ chuy·ªÉn ƒë·ªông kh√¥ng?
    int motionCountToday = 0;            // S·ªë l·∫ßn ph√°t hi·ªán trong ng√†y
    bool initialized = false;            // ƒê√£ kh·ªüi t·∫°o ch∆∞a?
    bool error = false;                  // C√≥ l·ªói kh√¥ng?
    unsigned long lastRead = 0;          // Th·ªùi ƒëi·ªÉm ƒë·ªçc l·∫ßn cu·ªëi
} pirData;                               // T√™n bi·∫øn: pirData

// ===== D·ªÆ LI·ªÜU C√ÇN (Load Cell) =====
// D√πng ƒë·ªÉ c√¢n tr·ªçng l∆∞·ª£ng t·ªï ong
struct
{
    long lastMeasureTime = 0;                     // Th·ªùi ƒëi·ªÉm ƒëo l·∫ßn cu·ªëi
    long lastUploadTime = 0;                      // Th·ªùi ƒëi·ªÉm upload l·∫ßn cu·ªëi
    float currentWeight = 0;                      // Tr·ªçng l∆∞·ª£ng hi·ªán t·∫°i (gram)
    float lastWeight = 0;                         // Tr·ªçng l∆∞·ª£ng l·∫ßn ƒëo tr∆∞·ªõc
    float calibrationValue = DEFAULT_CALIBRATION; // Gi√° tr·ªã hi·ªáu chu·∫©n c√¢n
    bool initialized = false;                     // ƒê√£ kh·ªüi t·∫°o ch∆∞a?
    bool error = false;                           // C√≥ l·ªói kh√¥ng?
    unsigned long lastRead = 0;                   // Th·ªùi ƒëi·ªÉm ƒë·ªçc l·∫ßn cu·ªëi
} weightData;                                     // T√™n bi·∫øn: weightData

// ===== TR·∫†NG TH√ÅI H·ªÜ TH·ªêNG =====
// L∆∞u th√¥ng tin chung v·ªÅ h·ªá th·ªëng
struct
{
    time_t lastResetTime = 0;             // Th·ªùi ƒëi·ªÉm reset l·∫ßn cu·ªëi
    struct tm timeinfo;                   // Th√¥ng tin th·ªùi gian hi·ªán t·∫°i
    unsigned long lastDeepSleepCheck = 0; // Th·ªùi ƒëi·ªÉm ki·ªÉm tra deep sleep
    bool lowPowerMode = false;            // Ch·∫ø ƒë·ªô ti·∫øt ki·ªám pin
    unsigned long uptimeSeconds = 0;      // T·ªïng th·ªùi gian ho·∫°t ƒë·ªông (gi√¢y)
    unsigned long lastUptimeUpdate = 0;   // Th·ªùi ƒëi·ªÉm c·∫≠p nh·∫≠t uptime
    bool timeInitialized = false;         // ƒê√£ ƒë·ªìng b·ªô gi·ªù ch∆∞a?
    bool eepromInitialized = false;       // ƒê√£ kh·ªüi t·∫°o EEPROM ch∆∞a?
} systemState;                            // T√™n bi·∫øn: systemState

// ===== KHAI B√ÅO C√ÅC H√ÄM (Function Prototypes) =====
// Khai b√°o c√°c h√†m tr∆∞·ªõc khi s·ª≠ d·ª•ng (ƒë·ªÉ compiler bi·∫øt)

// H√†m watchdog
void setupWatchdog(); // Kh·ªüi t·∫°o watchdog timer
void feedWatchdog();  // "Nu√¥i" watchdog (tr√°nh reset)

// H√†m k·∫øt n·ªëi
void setupWiFi();     // K·∫øt n·ªëi WiFi
void setupFirebase(); // K·∫øt n·ªëi Firebase

// H√†m th·ªùi gian
String getISOTimestamp(); // L·∫•y th·ªùi gian ƒë·ªãnh d·∫°ng ISO
bool initTime();          // ƒê·ªìng b·ªô th·ªùi gian t·ª´ internet

// H√†m BMP180
bool setupBMP180();      // Kh·ªüi t·∫°o c·∫£m bi·∫øn BMP180
void readBMP180();       // ƒê·ªçc d·ªØ li·ªáu BMP180
void uploadBMP180Data(); // Upload d·ªØ li·ªáu l√™n Firebase

// H√†m IR
void setupIRSensor();                           // Kh·ªüi t·∫°o c·∫£m bi·∫øn IR
void readIRSensor();                            // ƒê·ªçc c·∫£m bi·∫øn IR
void uploadActivityCount(bool isDaily = false); // Upload s·ªë l∆∞·ª£t ong

// H√†m Load Cell (c√¢n)
void setupHX711();                       // Kh·ªüi t·∫°o c√¢n
void readLoadCell();                     // ƒê·ªçc tr·ªçng l∆∞·ª£ng
void uploadWeight(bool isDaily = false); // Upload tr·ªçng l∆∞·ª£ng
void calibrateLoadCell();                // Hi·ªáu chu·∫©n c√¢n
void loadCalibrationData();              // T·∫£i d·ªØ li·ªáu hi·ªáu chu·∫©n
void saveCalibrationData();              // L∆∞u d·ªØ li·ªáu hi·ªáu chu·∫©n

// H√†m PIR
void setupPIR();         // Kh·ªüi t·∫°o c·∫£m bi·∫øn PIR
bool checkValidMotion(); // Ki·ªÉm tra chuy·ªÉn ƒë·ªông h·ª£p l·ªá
void handleMotion();     // X·ª≠ l√Ω chuy·ªÉn ƒë·ªông

// H√†m h·ªá th·ªëng
void updateSystemStatus();   // C·∫≠p nh·∫≠t tr·∫°ng th√°i h·ªá th·ªëng
void checkDailyReset();      // Ki·ªÉm tra reset h√†ng ng√†y
void printSystemStatus();    // In tr·∫°ng th√°i ra Serial
void handleSerialCommands(); // X·ª≠ l√Ω l·ªánh t·ª´ Serial Monitor

// H√†m kh√°c
void calibrateIRSensor();               // Hi·ªáu chu·∫©n c·∫£m bi·∫øn IR
void testAllSensors();                  // Ki·ªÉm tra t·∫•t c·∫£ c·∫£m bi·∫øn
void processData(AsyncResult &aResult); // X·ª≠ l√Ω d·ªØ li·ªáu t·ª´ Firebase

// ===== H√ÄM SETUP - CH·∫†Y M·ªòT L·∫¶N KHI B·∫¨T NGU·ªíN =====
void setup()
{
    // Kh·ªüi t·∫°o Serial ƒë·ªÉ in th√¥ng tin ra m√†n h√¨nh
    Serial.begin(115200); // T·ªëc ƒë·ªô 115200 baud
    delay(1000);          // Ch·ªù Serial kh·ªüi ƒë·ªông

    Serial.println("\n=== H·ªá Th·ªëng Gi√°m S√°t T·ªï Ong Th√¥ng Minh ===");

    // B∆Ø·ªöC 1: Kh·ªüi t·∫°o EEPROM (b·ªô nh·ªõ l∆∞u tr·ªØ vƒ©nh vi·ªÖn)
    EEPROM.begin(EEPROM_SIZE); // C·∫•p ph√°t 512 bytes cho EEPROM

    // B∆Ø·ªöC 2: Kh·ªüi t·∫°o giao ti·∫øp I2C (cho BMP180)
    Wire.begin(); // B·∫Øt ƒë·∫ßu I2C v·ªõi ch√¢n m·∫∑c ƒë·ªãnh (SDA=21, SCL=22)
    delay(100);   // Ch·ªù I2C ·ªïn ƒë·ªãnh

    // B∆Ø·ªöC 3: Kh·ªüi t·∫°o Watchdog Timer (gi√°m s√°t h·ªá th·ªëng)
    setupWatchdog(); // N·∫øu ch∆∞∆°ng tr√¨nh treo > 30s s·∫Ω t·ª± reset

    // B∆Ø·ªöC 4: T·∫£i d·ªØ li·ªáu hi·ªáu chu·∫©n t·ª´ EEPROM
    loadCalibrationData(); // ƒê·ªçc gi√° tr·ªã hi·ªáu chu·∫©n c√¢n v√† IR ƒë√£ l∆∞u

    // B∆Ø·ªöC 5: Kh·ªüi t·∫°o t·∫•t c·∫£ c√°c c·∫£m bi·∫øn
    setupBMP180();   // Kh·ªüi t·∫°o c·∫£m bi·∫øn nhi·ªát ƒë·ªô & √°p su·∫•t
    setupIRSensor(); // Kh·ªüi t·∫°o c·∫£m bi·∫øn ƒë·∫øm ong
    setupHX711();    // Kh·ªüi t·∫°o c√¢n ƒëi·ªán t·ª≠
    setupPIR();      // Kh·ªüi t·∫°o c·∫£m bi·∫øn chuy·ªÉn ƒë·ªông

    // B∆Ø·ªöC 6: K·∫øt n·ªëi WiFi v√† Firebase
    setupWiFi();                       // K·∫øt n·ªëi v√†o m·∫°ng WiFi
    if (WiFi.status() == WL_CONNECTED) // N·∫øu WiFi ƒë√£ k·∫øt n·ªëi
    {
        initTime();      // ƒê·ªìng b·ªô th·ªùi gian t·ª´ internet
        setupFirebase(); // K·∫øt n·ªëi Firebase
    }

    Serial.println("\nH·ªá th·ªëng ƒë√£ kh·ªüi t·∫°o v√† s·∫µn s√†ng!");

    // B∆Ø·ªöC 7: Hi·ªÉn th·ªã danh s√°ch l·ªánh c√≥ th·ªÉ d√πng
    Serial.println("\n===== C√ÅC L·ªÜNH ƒêI·ªÄU KHI·ªÇN =====");
    Serial.println("h - Hi·ªÉn th·ªã tr·ª£ gi√∫p");
    Serial.println("s - Xem tr·∫°ng th√°i h·ªá th·ªëng");
    Serial.println("r - ƒê·∫∑t l·∫°i b·ªô ƒë·∫øm v·ªÅ 0");
    Serial.println("t - Tare c√¢n (ƒë·∫∑t v·ªÅ 0)");
    Serial.println("c - Hi·ªáu chu·∫©n c√¢n");
    Serial.println("i - Hi·ªáu chu·∫©n c·∫£m bi·∫øn IR");
    Serial.println("u - T·∫£i t·∫•t c·∫£ d·ªØ li·ªáu l√™n Firebase");
    Serial.println("w - K·∫øt n·ªëi l·∫°i WiFi");
    Serial.println("f - K·∫øt n·ªëi l·∫°i Firebase");
    Serial.println("z - Ki·ªÉm tra t·∫•t c·∫£ c·∫£m bi·∫øn");
}

// ===== H√ÄM LOOP - CH·∫†Y LI√äN T·ª§C SAU KHI SETUP =====
// H√†m n√†y l·∫∑p l·∫°i m√£i m√£i, ƒë·ªçc c·∫£m bi·∫øn v√† upload d·ªØ li·ªáu
void loop()
{
    // ===== B∆Ø·ªöC 1: "Nu√¥i" Watchdog =====
    // B√°o cho Watchdog bi·∫øt ch∆∞∆°ng tr√¨nh v·∫´n ch·∫°y b√¨nh th∆∞·ªùng
    feedWatchdog(); // N·∫øu kh√¥ng g·ªçi h√†m n√†y, ESP32 s·∫Ω reset sau 30 gi√¢y

    // ===== B∆Ø·ªöC 2: C·∫≠p nh·∫≠t th·ªùi gian ho·∫°t ƒë·ªông =====
    if (millis() - systemState.lastUptimeUpdate >= 60000) // M·ªói 60 gi√¢y (1 ph√∫t)
    {
        systemState.uptimeSeconds += 60;         // TƒÉng uptime th√™m 60 gi√¢y
        systemState.lastUptimeUpdate = millis(); // C·∫≠p nh·∫≠t th·ªùi ƒëi·ªÉm
    }

    // ===== B∆Ø·ªöC 3: ƒê·ªçc t·∫•t c·∫£ c√°c c·∫£m bi·∫øn =====
    readBMP180();   // ƒê·ªçc nhi·ªát ƒë·ªô & √°p su·∫•t
    readIRSensor(); // ƒê·∫øm ong ra v√†o
    readLoadCell(); // ƒê·ªçc tr·ªçng l∆∞·ª£ng t·ªï
    handleMotion(); // Ki·ªÉm tra chuy·ªÉn ƒë·ªông (x√¢m nh·∫≠p)

    // ===== B∆Ø·ªöC 4: X·ª≠ l√Ω Firebase =====
    app.loop(); // Duy tr√¨ k·∫øt n·ªëi v·ªõi Firebase

    // N·∫øu ƒë√£ k·∫øt n·ªëi Firebase th√†nh c√¥ng
    if (app.ready())
    {
        firebaseConnected = true; // ƒê√°nh d·∫•u ƒë√£ k·∫øt n·ªëi

        // Upload nhi·ªát ƒë·ªô & √°p su·∫•t m·ªói 10 ph√∫t
        if (millis() - bmpData.lastUpload >= 600000) // 600000ms = 10 ph√∫t
        {
            uploadBMP180Data();            // G·ª≠i d·ªØ li·ªáu l√™n Firebase
            bmpData.lastUpload = millis(); // L∆∞u th·ªùi ƒëi·ªÉm upload
        }

        // Upload s·ªë l∆∞·ª£t ong ra v√†o m·ªói 1 ph√∫t
        if (millis() - irData.lastUploadTime >= CURRENT_UPDATE_INTERVAL) // 60000ms = 1 ph√∫t
        {
            uploadActivityCount(); // G·ª≠i s·ªë ƒë·∫øm l√™n Firebase
        }

        // Upload tr·ªçng l∆∞·ª£ng m·ªói 1 gi·ªù
        if (millis() - weightData.lastUploadTime >= UPLOAD_INTERVAL) // 3600000ms = 1 gi·ªù
        {
            uploadWeight();                       // G·ª≠i tr·ªçng l∆∞·ª£ng l√™n Firebase
            weightData.lastUploadTime = millis(); // L∆∞u th·ªùi ƒëi·ªÉm upload
        }

        // Upload tr·∫°ng th√°i h·ªá th·ªëng m·ªói 15 ph√∫t
        if (millis() - systemState.lastUptimeUpdate >= 900000) // 900000ms = 15 ph√∫t
        {
            updateSystemStatus();                    // G·ª≠i tr·∫°ng th√°i l√™n Firebase
            systemState.lastUptimeUpdate = millis(); // L∆∞u th·ªùi ƒëi·ªÉm
        }
    }

    // ===== B∆Ø·ªöC 5: Ki·ªÉm tra reset h√†ng ng√†y =====
    checkDailyReset(); // N·∫øu qua 0h s·∫Ω reset b·ªô ƒë·∫øm

    // ===== B∆Ø·ªöC 6: In tr·∫°ng th√°i ra Serial m·ªói 60 gi√¢y =====
    static unsigned long lastPrintStatus = 0; // Bi·∫øn static gi·ªØ gi√° tr·ªã gi·ªØa c√°c l·∫ßn g·ªçi
    if (millis() - lastPrintStatus >= 60000)  // M·ªói 60 gi√¢y
    {
        printSystemStatus();        // In th√¥ng tin ra Serial Monitor
        lastPrintStatus = millis(); // C·∫≠p nh·∫≠t th·ªùi ƒëi·ªÉm
    }

    // ===== B∆Ø·ªöC 7: X·ª≠ l√Ω l·ªánh t·ª´ Serial Monitor =====
    handleSerialCommands(); // Ki·ªÉm tra c√≥ l·ªánh n√†o t·ª´ ng∆∞·ªùi d√πng kh√¥ng (h, s, r, t, c...)

    // ===== B∆Ø·ªöC 8: Gi√°m s√°t k·∫øt n·ªëi WiFi =====
    static unsigned long lastWifiCheck = 0; // Th·ªùi ƒëi·ªÉm ki·ªÉm tra l·∫ßn cu·ªëi
    if (millis() - lastWifiCheck > 300000)  // M·ªói 5 ph√∫t (300000ms)
    {
        if (WiFi.status() != WL_CONNECTED) // N·∫øu WiFi b·ªã m·∫•t
        {
            Serial.println("‚ö†Ô∏è  WiFi m·∫•t k·∫øt n·ªëi! ƒêang th·ª≠ k·∫øt n·ªëi l·∫°i...");
            WiFi.disconnect(); // Ng·∫Øt k·∫øt n·ªëi c≈©
            setupWiFi();       // K·∫øt n·ªëi l·∫°i

            // N·∫øu WiFi OK nh∆∞ng Firebase ch∆∞a k·∫øt n·ªëi
            if (WiFi.status() == WL_CONNECTED && !firebaseConnected)
            {
                setupFirebase(); // K·∫øt n·ªëi l·∫°i Firebase
            }
        }
        lastWifiCheck = millis(); // C·∫≠p nh·∫≠t th·ªùi ƒëi·ªÉm ki·ªÉm tra
    }

    // ===== B∆Ø·ªöC 9: Ch·ªù m·ªôt ch√∫t =====
    delay(10); // D·ª´ng 10ms ƒë·ªÉ CPU kh√¥ng b·ªã qu√° t·∫£i (tr√°nh n√≥ng)
}

void setupWatchdog()
{
    esp_task_wdt_init(WDT_TIMEOUT, true); // B·∫≠t panic khi timeout
    esp_task_wdt_add(NULL);               // Th√™m lu·ªìng hi·ªán t·∫°i v√†o WDT
    Serial.println("Watchdog ƒë√£ kh·ªüi t·∫°o");
}

void feedWatchdog()
{
    esp_task_wdt_reset(); // Nu√¥i watchdog
}

void setupWiFi()
{
    WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
    Serial.print("Connecting to Wi-Fi");
    while (WiFi.status() != WL_CONNECTED)
    {
        Serial.print(".");
        delay(300);
    }
    Serial.println();

    ssl_client.setInsecure();
    ssl_client.setTimeout(1000);
    ssl_client.setHandshakeTimeout(5);
}

void setupFirebase()
{
    initializeApp(aClient, app, getAuth(user_auth), processData, "üîê authTask");
    app.getApp<RealtimeDatabase>(Database);
    Database.url(DATABASE_URL);
}

String getISOTimestamp()
{
    if (!getLocalTime(&systemState.timeinfo))
    {
        return "time-error";
    }
    char timestamp[25];
    strftime(timestamp, sizeof(timestamp), "%Y-%m-%d_%H:%M:%S", &systemState.timeinfo);
    return String(timestamp);
}

void loadCalibrationData()
{
    Serial.println("ƒêang t·∫£i d·ªØ li·ªáu hi·ªáu chu·∫©n...");

    // Ki·ªÉm tra EEPROM ƒë√£ ƒë∆∞·ª£c kh·ªüi t·∫°o ch∆∞a
    uint16_t magicNumber;
    EEPROM.get(EEPROM_INITIALIZED_ADDR, magicNumber);

    if (magicNumber != EEPROM_MAGIC_NUMBER)
    {
        Serial.println("EEPROM ch∆∞a kh·ªüi t·∫°o, s·ª≠ d·ª•ng gi√° tr·ªã m·∫∑c ƒë·ªãnh");
        // ƒê·∫∑t gi√° tr·ªã m·∫∑c ƒë·ªãnh cho hi·ªáu chu·∫©n
        weightData.calibrationValue = DEFAULT_CALIBRATION;

        // Kh·ªüi t·∫°o ng∆∞·ª°ng v·ªõi gi√° tr·ªã m·∫∑c ƒë·ªãnh
        irData.thresholds = DEFAULT_IR_THRESHOLDS;

        // ƒê√°nh d·∫•u EEPROM ƒë√£ kh·ªüi t·∫°o
        EEPROM.put(EEPROM_INITIALIZED_ADDR, (uint16_t)EEPROM_MAGIC_NUMBER);
        EEPROM.commit();
        return;
    }

    // T·∫£i gi√° tr·ªã hi·ªáu chu·∫©n Load Cell
    EEPROM.get(LOADCELL_CAL_ADDR, weightData.calibrationValue);

    // X√°c th·ª±c gi√° tr·ªã hi·ªáu chu·∫©n
    if (isnan(weightData.calibrationValue) || weightData.calibrationValue < 10 || weightData.calibrationValue > 10000)
    {
        Serial.println("Hi·ªáu chu·∫©n load cell kh√¥ng h·ª£p l·ªá, s·ª≠ d·ª•ng gi√° tr·ªã m·∫∑c ƒë·ªãnh");
        weightData.calibrationValue = DEFAULT_CALIBRATION;
    }
    else
    {
        Serial.printf("ƒê√£ t·∫£i hi·ªáu chu·∫©n load cell: %.1f\n", weightData.calibrationValue);
    }

    // T·∫£i ng∆∞·ª°ng c·∫£m bi·∫øn IR
    EEPROM.get(IR_THRESHOLDS_ADDR, irData.thresholds);

    // X√°c th·ª±c ng∆∞·ª°ng
    if (isnan(irData.thresholds) || irData.thresholds < 100 || irData.thresholds > 3500)
    {
        Serial.println("Ng∆∞·ª°ng IR kh√¥ng h·ª£p l·ªá, s·ª≠ d·ª•ng gi√° tr·ªã m·∫∑c ƒë·ªãnh");
        irData.thresholds = DEFAULT_IR_THRESHOLDS;
    }
    else
    {
        Serial.print("ƒê√£ t·∫£i ng∆∞·ª°ng c·∫£m bi·∫øn IR");
    }
    systemState.eepromInitialized = true;
}

void saveCalibrationData()
{
    // L∆∞u hi·ªáu chu·∫©n load cell
    EEPROM.put(LOADCELL_CAL_ADDR, weightData.calibrationValue);

    // L∆∞u ng∆∞·ª°ng IR
    EEPROM.put(IR_THRESHOLDS_ADDR, irData.thresholds);

    // ƒê√°nh d·∫•u EEPROM ƒë√£ kh·ªüi t·∫°o
    EEPROM.put(EEPROM_INITIALIZED_ADDR, (uint16_t)EEPROM_MAGIC_NUMBER);

    // Commit thay ƒë·ªïi
    if (EEPROM.commit())
    {
        Serial.println("D·ªØ li·ªáu hi·ªáu chu·∫©n ƒë√£ l∆∞u v√†o EEPROM");
    }
    else
    {
        Serial.println("L·ªñI: Kh√¥ng th·ªÉ l∆∞u d·ªØ li·ªáu hi·ªáu chu·∫©n");
    }
}

bool initTime()
{
    configTime(GMT_OFFSET_SEC, DAYLIGHT_OFFSET_SEC, NTP_SERVER);
    if (!getLocalTime(&systemState.timeinfo))
    {
        Serial.println("Failed to obtain time!");
        return false;
    }

    time(&systemState.lastResetTime);
    systemState.timeInitialized = true;

    char timeStr[30];
    strftime(timeStr, sizeof(timeStr), "%A, %B %d %Y %H:%M:%S", &systemState.timeinfo);
    Serial.print("Current time: ");
    Serial.println(timeStr);

    return true;
}

// Kh·ªüi t·∫°o BMP180
bool setupBMP180()
{
    Serial.print("ƒêang kh·ªüi t·∫°o BMP180... ");
    if (!bmp.begin(BMP_I2C_ADDR))
    {
        Serial.println("Th·∫•t b·∫°i! Ki·ªÉm tra ƒë·∫•u n·ªëi.");
        bmpData.error = true;
        return false;
    }
    bmpData.initialized = true;
    Serial.println("Th√†nh c√¥ng!");
    return true;
}

void setupIRSensor()
{
    irData.initialized = true;
    return;
}

void setupHX711()
{
    Serial.print("ƒêang kh·ªüi t·∫°o HX711... ");

    loadcell.begin(LOADCELL_DOUT_PIN, LOADCELL_SCK_PIN);

    if (!loadcell.wait_ready_timeout(2000))
    {
        Serial.println("Th·∫•t b·∫°i! Ki·ªÉm tra ƒë·∫•u n·ªëi.");
        weightData.error = true;
        return;
    }

    // S·ª≠ d·ª•ng gi√° tr·ªã hi·ªáu chu·∫©n ƒë√£ t·∫£i
    loadcell.set_scale(weightData.calibrationValue);
    Serial.printf("S·ª≠ d·ª•ng hi·ªáu chu·∫©n: %.1f\n", weightData.calibrationValue);

    // Tare c√¢n
    loadcell.tare();

    weightData.initialized = true;
    Serial.println("Th√†nh c√¥ng!");
}

void setupPIR()
{
    Serial.print("ƒêang kh·ªüi t·∫°o c·∫£m bi·∫øn PIR... ");
    pinMode(PIR_PIN, INPUT);
    pinMode(LED_PIN, OUTPUT);
    digitalWrite(LED_PIN, LOW);

    // Kh·ªüi t·∫°o l·ªãch s·ª≠ ƒë·ªçc
    for (int i = 0; i < pirData.READING_HISTORY_SIZE; i++)
    {
        pirData.readingHistory[i] = false;
    }

    pirData.initialized = true;
    Serial.println("Th√†nh c√¥ng!");
    Serial.println("ƒêang ch·ªù PIR ·ªïn ƒë·ªãnh...");
    delay(2000);
    Serial.println("PIR ƒë√£ s·∫µn s√†ng!");
}

void readBMP180()
{
    unsigned long currentTime = millis();

    // ƒê·ªçc m·ªói 10 gi√¢y
    if (currentTime - bmpData.lastRead < 10000)
        return;
    bmpData.lastRead = currentTime;

    // Ki·ªÉm tra c·∫£m bi·∫øn ƒë√£ kh·ªüi t·∫°o
    if (!bmpData.initialized)
        return;

    // L∆∞u gi√° tr·ªã
    bmpData.temperature = bmp.readTemperature();
    bmpData.pressure = bmp.readPressure() / 100.0;
    if (isnan(bmpData.temperature) || isnan(bmpData.pressure))
    {
        Serial.println("L·ªói ƒë·ªçc BMP180!");
        bmpData.error = true;
        return;
    }
    bmpData.error = false;

    // X√°c th·ª±c gi√° tr·ªã ƒë·ªçc
    if (bmpData.temperature < TEMP_THRESHOLD_LOW || bmpData.temperature > TEMP_THRESHOLD_HIGH)
    {
        Serial.println("Nhi·ªát ƒë·ªô ngo√†i ph·∫°m vi d·ª± ki·∫øn!");
    }
}

void readIRSensor()
{
    unsigned long currentTime = millis();

    // Gi·ªõi h·∫°n t·∫ßn su·∫•t ƒë·ªçc ƒë·ªÉ tr√°nh CPU qu√° t·∫£i
    if (currentTime - irData.lastRead < 50)
        return;
    irData.lastRead = currentTime;

    // Ki·ªÉm tra c·∫£m bi·∫øn ƒë√£ kh·ªüi t·∫°o
    if (!irData.initialized)
        return;

    static int lastValue = 0;
    int currentValue;
    bool activity = false;

    // ƒê·ªçc gi√° tr·ªã c·∫£m bi·∫øn
    currentValue = analogRead(IR_PIN);
    if (abs(currentValue - lastValue) > irData.thresholds)
    {
        activity = true;
    }
    lastValue = currentValue;

    if (activity && (currentTime - irData.lastDetectionTime > DETECTION_DELAY))
    {
        irData.activityCount++;
        irData.lastDetectionTime = currentTime;
    }
}

void readLoadCell()
{
    unsigned long currentTime = millis();

    if (currentTime - weightData.lastRead < MEASURE_INTERVAL)
        return;
    weightData.lastRead = currentTime;

    // Ki·ªÉm tra c·∫£m bi·∫øn ƒë√£ kh·ªüi t·∫°o
    if (!weightData.initialized)
        return;

    if (!loadcell.wait_ready_timeout(100))
    {
        Serial.println("HX711 kh√¥ng ph·∫£n h·ªìi!");
        weightData.error = true;
        return;
    }

    weightData.error = false;

    float sum = 0;
    int validReadings = 0;

    for (int i = 0; i < SAMPLE_SIZE; i++)
    {
        float reading = loadcell.get_units();
        if (reading > -10000 && reading < 10000)
        { // Ki·ªÉm tra h·ª£p l√Ω
            sum += reading;
            validReadings++;
        }
        yield(); // Cho ph√©p c√°c t√°c v·ª• kh√°c ch·∫°y
    }

    if (validReadings == 0)
        return;

    float newWeight = sum / validReadings;

    // Ki·ªÉm tra thay ƒë·ªïi ƒë√°ng k·ªÉ
    if (abs(newWeight - weightData.lastWeight) > WEIGHT_CHANGE_ALERT)
    {
        Serial.printf("Thay ƒë·ªïi tr·ªçng l∆∞·ª£ng ƒë√°ng k·ªÉ: %.2f -> %.2f gam\n", weightData.lastWeight, newWeight);

        // Ghi c·∫£nh b√°o v√†o Firebase n·∫øu ƒë√£ k·∫øt n·ªëi
        if (firebaseConnected)
        {
            FirebaseJson json;
            json.set("previous_weight", weightData.lastWeight);
            json.set("current_weight", newWeight);
            json.set("change", newWeight - weightData.lastWeight);
            json.set("timesttamp", getISOTimestamp());

            Database.set<object_t>(aClient, "alerts/current/weight", object_t(json.raw()), processData, "alerts current weight");

            // C≈©ng ghi v√†o l·ªãch s·ª≠
            String historyPath = "alerts/history/";
            historyPath += getISOTimestamp();
            historyPath += "/weight";
            Database.set<object_t>(aClient, historyPath, object_t(json.raw()), processData, "alerts history weight");
        }
    }

    // C·∫≠p nh·∫≠t gi√° tr·ªã tr·ªçng l∆∞·ª£ng
    weightData.currentWeight = newWeight;
    weightData.lastWeight = weightData.currentWeight;
}

bool checkValidMotion()
{
    unsigned long currentTime = millis();

    // Th√™m gi√° tr·ªã ƒë·ªçc m·ªõi v√†o l·ªãch s·ª≠ m·ªói 100ms
    if (currentTime - pirData.lastReadingTime >= 100)
    {
        pirData.lastReadingTime = currentTime;
        pirData.readingHistory[pirData.readingIndex] = (digitalRead(PIR_PIN) == HIGH);
        pirData.readingIndex = (pirData.readingIndex + 1) % pirData.READING_HISTORY_SIZE;

        // ƒê·∫øm s·ªë l·∫ßn ƒë·ªçc HIGH trong l·ªãch s·ª≠
        int highCount = 0;
        for (int i = 0; i < pirData.READING_HISTORY_SIZE; i++)
        {
            if (pirData.readingHistory[i])
                highCount++;
        }

        // Chuy·ªÉn ƒë·ªông h·ª£p l·ªá n·∫øu ƒë·ªß s·ªë l·∫ßn ƒë·ªçc HIGH
        if (highCount >= SAMPLES_REQUIRED)
        {
            pirData.lastValidMotion = currentTime;
            return true;
        }
    }

    return (currentTime - pirData.lastValidMotion < DETECTION_WINDOW);
}

void handleMotion()
{
    unsigned long currentTime = millis();

    // Gi·ªõi h·∫°n t·∫ßn su·∫•t ki·ªÉm tra
    if (currentTime - pirData.lastRead < 50)
        return;
    pirData.lastRead = currentTime;

    // Ki·ªÉm tra c·∫£m bi·∫øn ƒë√£ kh·ªüi t·∫°o
    if (!pirData.initialized)
        return;

    bool validMotion = checkValidMotion();

    // B·∫Øt ƒë·∫ßu chuy·ªÉn ƒë·ªông
    if (validMotion && !pirData.motionActive)
    {
        pirData.motionActive = true;
        pirData.motionStartTime = currentTime;
        digitalWrite(LED_PIN, HIGH);

        // Ch·ªâ ƒë·∫øm l√† chuy·ªÉn ƒë·ªông m·ªõi n·∫øu ƒë√£ qua th·ªùi gian cooldown
        if (currentTime - pirData.lastMotionTime > MOTION_COOLDOWN)
        {
            pirData.motionCountToday++;
            pirData.lastMotionTime = currentTime;
        }
    }
    // K·∫øt th√∫c chuy·ªÉn ƒë·ªông
    else if (!validMotion && pirData.motionActive)
    {
        pirData.motionActive = false;
        digitalWrite(LED_PIN, LOW);

        // T√≠nh th·ªùi l∆∞·ª£ng chuy·ªÉn ƒë·ªông
        unsigned long duration = currentTime - pirData.motionStartTime;

        // Ch·ªâ ghi chuy·ªÉn ƒë·ªông ƒë√°ng k·ªÉ
        if (duration >= MOTION_DURATION_MIN)
        {
            // Ghi v√†o Firebase n·∫øu ƒë√£ k·∫øt n·ªëi
            if (firebaseConnected)
            {
                String timestamp = getISOTimestamp();
                FirebaseJson json;

                // C·∫≠p nh·∫≠t tr·∫°ng th√°i chuy·ªÉn ƒë·ªông hi·ªán t·∫°i
                json.set("active", true);
                json.set("last_detected", timestamp);
                json.set("count_today", pirData.motionCountToday);
                json.set("duration", (int)(duration / 1000)); // Chuy·ªÉn sang gi√¢y
                Database.set<object_t>(aClient, "alerts/current/motion", object_t(json.raw()), processData, "alerts current motion");

                // Ghi s·ª± ki·ªán chuy·ªÉn ƒë·ªông v√†o l·ªãch s·ª≠
                FirebaseJson eventJson;
                eventJson.set("time", timestamp);
                eventJson.set("duration", (int)(duration / 1000));
                eventJson.set("type", duration > MOTION_DURATION_MIN * 2 ? "significant" : "brief");

                char datePath[11];
                if (getLocalTime(&systemState.timeinfo))
                {
                    strftime(datePath, sizeof(datePath), "%Y-%m-%d", &systemState.timeinfo);
                    String path = "alerts/history/";
                    path += String(datePath);
                    path += "/motion_events";
                    Database.set<object_t>(aClient, path, object_t(eventJson.raw()), processData, "alerts history motion events");
                }
            }
        }
    }
}

void uploadBMP180Data()
{
    if (!firebaseConnected || bmpData.error)
        return;

    FirebaseJson json;
    String timestamp = getISOTimestamp();

    // Th√™m d·ªØ li·ªáu m√¥i tr∆∞·ªùng
    json.set("temperature", bmpData.temperature);
    json.set("pressure", bmpData.pressure);
    json.set("timestamp", timestamp);

    // C·∫≠p nh·∫≠t d·ªØ li·ªáu m√¥i tr∆∞·ªùng hi·ªán t·∫°i
    Database.set<object_t>(aClient, "environment/current/", object_t(json.raw()), processData, "BMP180 current");

    // C≈©ng ghi v√†o l·ªãch s·ª≠ theo gi·ªù
    char datePath[20];
    char hourPath[6];
    if (getLocalTime(&systemState.timeinfo))
    {
        strftime(datePath, sizeof(datePath), "%Y-%m-%d", &systemState.timeinfo);
        strftime(hourPath, sizeof(hourPath), "%H:00", &systemState.timeinfo);

        String historyPath = "environment/history/";
        historyPath += String(datePath);
        historyPath += "/hourly/";
        historyPath += String(hourPath);

        Database.set<object_t>(aClient, historyPath, object_t(json.raw()), processData, "BMP180 history");

        // T√≠nh v√† c·∫≠p nh·∫≠t trung b√¨nh theo ng√†y
        String dailyAvgPath = "environment/history/";
        dailyAvgPath += String(datePath);
        dailyAvgPath += "/daily_average";

        Database.get(aClient, dailyAvgPath, dbResult, false);

        FirebaseJson avgJson;
        FirebaseJson result;
        FirebaseJsonData data;

        float tempSum = bmpData.temperature;
        float presSum = bmpData.pressure;
        int count = 1;

        if (dbResult.available())
        {
            result.setJsonData(dbResult.c_str());

            result.get(data, "count");
            count = data.to<int>() + 1;

            result.get(data, "temp");
            tempSum += data.to<float>() * (count - 1);

            result.get(data, "pressure");
            presSum += data.to<float>() * (count - 1);
        }
        avgJson.set("temp", tempSum / count);
        avgJson.set("pressure", presSum / count);
        avgJson.set("count", count);
        avgJson.set("last_update", timestamp);

        Database.set<object_t>(aClient, dailyAvgPath, object_t(avgJson.raw()), processData, "avg bmp180");
    }
}

void uploadActivityCount(bool isDaily)
{
    if (!firebaseConnected)
        return;

    String timestamp = getISOTimestamp();
    FirebaseJson json;

    if (isDaily)
    {
        // L∆∞u s·ªë ƒë·∫øm h√†ng ng√†y v√†o d·ªØ li·ªáu l·ªãch s·ª≠
        char datePath[11];
        strftime(datePath, sizeof(datePath), "%Y-%m-%d", &systemState.timeinfo);

        json.set("total_count", irData.activityCount);
        json.set("timestamp", timestamp);

        String historyPath = "beeActivity/history/";
        historyPath += datePath;

        Database.set<object_t>(aClient, historyPath, object_t(json.raw()), processData, "bee activity history daily");
    }
    else
    {
        // C·∫≠p nh·∫≠t s·ªë ƒë·∫øm hi·ªán t·∫°i
        json.set("count", irData.activityCount);
        json.set("timestamp", timestamp);

        Database.set<object_t>(aClient, "beeActivity/current", object_t(json.raw()), processData, "bee activity current");

        // C≈©ng c·∫≠p nh·∫≠t s·ªë ƒë·∫øm theo gi·ªù trong c·∫•u tr√∫c ng√†y
        char datePath[11];
        char hourPath[6];
        if (getLocalTime(&systemState.timeinfo))
        {
            strftime(datePath, sizeof(datePath), "%Y-%m-%d", &systemState.timeinfo);
            strftime(hourPath, sizeof(hourPath), "%H:00", &systemState.timeinfo);

            String hourlyPath = "beeActivity/daily/";
            hourlyPath += String(datePath);
            hourlyPath += "/hourly_counts/";
            hourlyPath += String(hourPath);

            FirebaseJson hourlyJson;
            hourlyJson.set("count", irData.activityCount - irData.lastUploadedCount);
            Database.set<object_t>(aClient, hourlyPath, object_t(hourlyJson.raw()), processData, "be activity daily hourly count");

            // C·∫≠p nh·∫≠t t·ªïng s·ªë ƒë·∫øm
            FirebaseJson totalJson;
            totalJson.set("total_count", irData.activityCount);
            String totalPath = "beeActivity/daily/";
            totalPath += String(datePath);
            Database.update<object_t>(aClient, totalPath, object_t(totalJson.raw()), processData, "bee activity daily total count");
        }

        irData.lastUploadedCount = irData.activityCount;
        irData.lastUploadTime = millis();
    }
}

void uploadWeight(bool isDaily)
{
    if (!firebaseConnected || weightData.currentWeight <= 0)
        return;

    FirebaseJson json;
    String timestamp = getISOTimestamp();

    json.set("total_weight", weightData.currentWeight);
    json.set("net_weight", weightData.currentWeight - EMPTY_HIVE_WEIGHT);
    json.set("timestamp", timestamp);

    if (isDaily)
    {
        char datePath[20];
        strftime(datePath, sizeof(datePath), "%Y-%m-%d", &systemState.timeinfo);

        // L∆∞u trung b√¨nh theo ng√†y
        String historyPath = "weight/history/";
        historyPath += String(datePath);
        historyPath += "/daily_average";

        Database.set<object_t>(aClient, historyPath, object_t(json.raw()), processData, "weight history daily avg");

        // Th√™m ph√©p ƒëo v√†o c√°c ph√©p ƒëo trong ng√†y
        char timePath[6];
        strftime(timePath, sizeof(timePath), "%H:00", &systemState.timeinfo);

        FirebaseJson timeJson;
        timeJson.set(timePath, weightData.currentWeight);
        String measurementPath = historyPath;
        measurementPath += "/measurements";
        Database.set<object_t>(aClient, measurementPath, object_t(timeJson.raw()), processData, "weight history measurements");
    }
    else
    {
        // C·∫≠p nh·∫≠t tr·ªçng l∆∞·ª£ng hi·ªán t·∫°i
        Database.set<object_t>(aClient, "weight/current", object_t(json.raw()), processData, "weight current");
    }
}

void updateSystemStatus()
{
    if (!firebaseConnected)
        return;

    FirebaseJson statusJson;
    statusJson.set("last_update", getISOTimestamp());
    statusJson.set("uptime", systemState.uptimeSeconds);
    statusJson.set("wifi_strength", WiFi.RSSI());

    // Tr·∫°ng th√°i c·∫£m bi·∫øn
    FirebaseJson sensorStatus;

    // Ki·ªÉm tra BMP180
    sensorStatus.set("bmp180", !isnan(bmp.readTemperature()) ? "ok" : "error");

    // Ki·ªÉm tra HX711
    sensorStatus.set("loadcell", loadcell.wait_ready_timeout(100) ? "ok" : "error");

    statusJson.set("sensors", sensorStatus);

    // C·∫≠p nh·∫≠t tr·∫°ng th√°i
    Database.set<object_t>(aClient, "/system/status", object_t(statusJson.raw()), processData, "üìä statusUpdate");
}

void checkDailyReset()
{
    if (!getLocalTime(&systemState.timeinfo))
    {
        return;
    }
    time_t now;
    time(&now);

    // N·∫øu l√† ng√†y m·ªõi (qua n·ª≠a ƒë√™m) v√† ch∆∞a ƒë·∫∑t l·∫°i
    if (systemState.timeinfo.tm_hour == 0 && systemState.timeinfo.tm_min == 0 && difftime(now, systemState.lastResetTime) > 3600)
    { // ƒê·∫£m b·∫£o √≠t nh·∫•t 1 gi·ªù ƒë√£ tr√¥i qua
        // L∆∞u d·ªØ li·ªáu h√†ng ng√†y tr∆∞·ªõc khi ƒë·∫∑t l·∫°i
        uploadActivityCount(true);
        uploadWeight(true);

        // ƒê·∫∑t l·∫°i b·ªô ƒë·∫øm
        irData.activityCount = 0;
        irData.lastUploadedCount = 0;
        pirData.motionCountToday = 0;

        // C·∫≠p nh·∫≠t th·ªùi gian ƒë·∫∑t l·∫°i
        time(&systemState.lastResetTime);

        // ƒê·∫∑t l·∫°i tr·∫°ng th√°i chuy·ªÉn ƒë·ªông trong Firebase
        if (firebaseConnected)
        {
            FirebaseJson json;
            json.set("count_today", 0);
            json.set("active", false);

            Database.set<object_t>(aClient, "alerts/current/motion", object_t(json.raw()), processData, "reset motion status");
        }

        Serial.println("ƒê√£ ho√†n t·∫•t ƒë·∫∑t l·∫°i b·ªô ƒë·∫øm h√†ng ng√†y");
    }
}

void calibrateLoadCell()
{
    Serial.println("\n*** B·∫Øt ƒë·∫ßu hi·ªáu chu·∫©n Load Cell ***");
    Serial.println("1. Lo·∫°i b·ªè t·∫•t c·∫£ tr·ªçng l∆∞·ª£ng v√† nh·∫•n Enter");

    while (!Serial.available())
    {
        feedWatchdog();
        delay(100);
    }

    Serial.read();

    loadcell.tare();
    Serial.println("C√¢n ƒë√£ ƒë∆∞·ª£c tare v·ªÅ 0");
    delay(1000);

    Serial.println("2. ƒê·∫∑t m·ªôt tr·ªçng l∆∞·ª£ng ƒë√£ bi·∫øt (v√≠ d·ª•: 300 gram) v√† nh·∫≠p tr·ªçng l∆∞·ª£ng b·∫±ng gram:");
    String input = "";
    while (!Serial.available())
    {
        feedWatchdog();
        delay(100);
    }

    while (Serial.available())
    {
        char c = Serial.read();
        if (c == '\n' || c == '\r')
            break;
        input += c;
        delay(10);
    }

    float calibrationWeight = input.toFloat();
    if (calibrationWeight <= 0)
    {
        Serial.println("Tr·ªçng l∆∞·ª£ng kh√¥ng h·ª£p l·ªá! H·ªßy hi·ªáu chu·∫©n.");
        return;
    }

    long rawValue = loadcell.read_average(10);
    float newCalibrationValue = rawValue / calibrationWeight;

    // Ki·ªÉm tra t√≠nh h·ª£p l·ªá
    if (newCalibrationValue < 100 || newCalibrationValue > 10000)
    {
        Serial.println("Gi√° tr·ªã hi·ªáu chu·∫©n ngo√†i ph·∫°m vi! H·ªßy hi·ªáu chu·∫©n.");
        return;
    }

    loadcell.set_scale(newCalibrationValue);
    weightData.calibrationValue = newCalibrationValue;

    // X√°c minh hi·ªáu chu·∫©n
    float verificationWeight = loadcell.get_units(10);
    Serial.printf("Tr·ªçng l∆∞·ª£ng ƒëo ƒë∆∞·ª£c: %.2f gam (ph·∫£i g·∫ßn v·ªõi %.2fg)\n",
                  verificationWeight, calibrationWeight);
    Serial.printf("Gi√° tr·ªã hi·ªáu chu·∫©n: %.1f\n", newCalibrationValue);

    // L∆∞u v√†o EEPROM
    saveCalibrationData();
}

void calibrateIRSensor()
{
    Serial.println("\n*** B·∫Øt ƒë·∫ßu hi·ªáu chu·∫©n c·∫£m bi·∫øn IR ***");
    Serial.println("ƒê·∫∑t m·ªôt v·∫≠t tr·∫Øng/ph·∫£n quang ~2cm d∆∞·ªõi c·∫£m bi·∫øn v√† nh·∫•n Enter");

    // while (!Serial.available()) {
    //     feedWatchdog();
    //     delay(100);
    // }
    // Serial.read();

    Serial.println("\nHi·ªáu chu·∫©n c·∫£m bi·∫øn IR ho√†n t·∫•t!");
    Serial.println("Di chuy·ªÉn v·∫≠t d∆∞·ªõi c·∫£m bi·∫øn ƒë·ªÉ ki·ªÉm tra ph√°t hi·ªán.");
}

void printSystemStatus()
{
    Serial.println("\n=== System Status ===");
    Serial.print("Time: ");
    Serial.println(getISOTimestamp());

    Serial.println("\nSensor Readings:");
    Serial.printf("  BMP180:  Temp: %.2f¬∞C, Pressure: %.2fhPa\n", bmpData.temperature, bmpData.pressure);
    Serial.printf("  Activity: %d bee movements today\n", irData.activityCount);
    Serial.printf("  Weight:   %.2fgam (Net: %.2fgam)\n", weightData.currentWeight, weightData.currentWeight - EMPTY_HIVE_WEIGHT);
    Serial.printf("  Motion:   %d detections today, Currently %s\n", pirData.motionCountToday, pirData.motionActive ? "ACTIVE" : "inactive");

    Serial.println("\nConnectivity:");
    Serial.printf("  WiFi: %s (RSSI: %ddBm)\n", WiFi.status() == WL_CONNECTED ? "Connected" : "Disconnected", WiFi.RSSI());
    Serial.printf("  Firebase: %s\n", firebaseConnected ? "Connected" : "Disconnected");

    Serial.printf("  System uptime: %lu minutes\n", systemState.uptimeSeconds / 60);

    Serial.println("\nSensor Health:");
    Serial.printf("  BMP180: %s\n", bmpData.error ? "ERROR" : "OK");
    Serial.printf("  IR Sensors: %s\n", irData.error ? "ERROR" : "OK");
    Serial.printf("  Load Cell: %s\n", weightData.error ? "ERROR" : "OK");
    Serial.printf("  PIR Sensor: %s\n", pirData.error ? "ERROR" : "OK");
    Serial.println("=====================\n");
}

void testAllSensors()
{
    Serial.println("\n=== B·∫Øt ƒë·∫ßu ki·ªÉm tra c·∫£m bi·∫øn ===");

    // Ki·ªÉm tra BMP180

    // Ki·ªÉm tra IR

    // Ki·ªÉm tra HX711

    // Ki·ªÉm tra PIR

    Serial.println("=== Ho√†n t·∫•t ki·ªÉm tra c·∫£m bi·∫øn ===\n");
}

void handleSerialCommands()
{
    if (Serial.available())
    {
        char cmd = Serial.read();

        switch (cmd)
        {
        case 'h': // Tr·ª£ gi√∫p
            Serial.println("\nC√°c l·ªánh kh·∫£ d·ª•ng:");
            Serial.println("h - Tr·ª£ gi√∫p");
            Serial.println("s - Tr·∫°ng th√°i h·ªá th·ªëng");
            Serial.println("r - ƒê·∫∑t l·∫°i b·ªô ƒë·∫øm");
            Serial.println("t - Tare c·∫£m bi·∫øn t·∫£i");
            Serial.println("c - Hi·ªáu chu·∫©n c·∫£m bi·∫øn t·∫£i");
            Serial.println("i - Hi·ªáu chu·∫©n c·∫£m bi·∫øn IR");
            Serial.println("u - T·∫£i l√™n t·∫•t c·∫£ d·ªØ li·ªáu hi·ªán t·∫°i");
            Serial.println("w - K·∫øt n·ªëi l·∫°i WiFi");
            Serial.println("f - K·∫øt n·ªëi l·∫°i Firebase");
            Serial.println("z - Ki·ªÉm tra c·∫£m bi·∫øn");
            break;

        case 's': // Tr·∫°ng th√°i
            printSystemStatus();
            break;

        case 'r': // ƒê·∫∑t l·∫°i b·ªô ƒë·∫øm
            irData.activityCount = 0;
            irData.lastUploadedCount = 0;
            pirData.motionCountToday = 0;
            Serial.print("ƒê√£ ƒë·∫∑t l·∫°i b·ªô ƒë·∫øm v·ªÅ 0");
            break;

        case 't': // Tare
            loadcell.tare();
            Serial.println("ƒê√£ tare c·∫£m bi·∫øn t·∫£i v·ªÅ 0");
            break;

        case 'c': // Hi·ªáu chu·∫©n loadcell
            calibrateLoadCell();
            break;

        case 'i': // Hi·ªáu chu·∫©n IR
            calibrateIRSensor();
            break;

        case 'u': // T·∫£i l√™n t·∫•t c·∫£
            if (firebaseConnected)
            {
                uploadBMP180Data();
                uploadActivityCount();
                uploadWeight();
                updateSystemStatus();
                Serial.println("ƒê√£ t·∫£i l√™n t·∫•t c·∫£ d·ªØ li·ªáu l√™n Firebase");
            }
            else
            {
                Serial.println("Firebase ch∆∞a k·∫øt n·ªëi. K·∫øt n·ªëi tr∆∞·ªõc v·ªõi l·ªánh 'f'");
            }
            break;

        case 'w': // K·∫øt n·ªëi l·∫°i WiFi
            WiFi.disconnect();
            delay(1000);
            setupWiFi();
            break;

        case 'f': // K·∫øt n·ªëi l·∫°i Firebase
            if (WiFi.status() == WL_CONNECTED)
            {
                setupFirebase();
            }
            else
            {
                Serial.println("WiFi ch∆∞a k·∫øt n·ªëi. K·∫øt n·ªëi WiFi tr∆∞·ªõc v·ªõi l·ªánh 'w'");
            }
            break;

        case 'z': // Ki·ªÉm tra c·∫£m bi·∫øn
            testAllSensors();
            break;
        }

        // X√≥a c√°c k√Ω t·ª± c√≤n l·∫°i
        while (Serial.available())
        {
            Serial.read();
        }
    }
}

void processData(AsyncResult &aResult)
{
    if (!aResult.isResult())
        return;

    if (aResult.isEvent())
        Firebase.printf("Event task: %s, msg: %s, code: %d\n", aResult.uid().c_str(), aResult.eventLog().message().c_str(), aResult.eventLog().code());

    if (aResult.isDebug())
        Firebase.printf("Debug task: %s, msg: %s\n", aResult.uid().c_str(), aResult.debug().c_str());

    if (aResult.isError())
        Firebase.printf("Error task: %s, msg: %s, code: %d\n", aResult.uid().c_str(), aResult.error().message().c_str(), aResult.error().code());

    if (aResult.available())
        Firebase.printf("task: %s, payload: %s\n", aResult.uid().c_str(), aResult.c_str());
}